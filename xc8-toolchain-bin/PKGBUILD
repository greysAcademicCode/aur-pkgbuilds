_number_of_bits=8
pkgname=xc${_number_of_bits}-toolchain-bin
pkgver=1.34
pkgrel=4
pkgdesc="Microchip's MPLAB XC${_number_of_bits} C compiler toolchain for their PIC10/12/16/18 microcontroller families and their PIC14000 device"
arch=(i686 x86_64)
url=http://www.microchip.com/xc${_number_of_bits}
license=(custom)
if [[ $CARCH = i686 ]]; then
  depends=(
    'expat'
    'gcc-libs'
  )
else
  depends=(
    'lib32-expat'
    'lib32-gcc-libs'
  )
fi
provides=(mplabxc${_number_of_bits})
conflicts=(microchip-mplabxc${_number_of_bits}-bin)
makedepends=(tcl-tabs sdx)
options=(!strip docs libtool emptydirs !zipman staticlibs !upx)
source=("installerBlobFromMicrochip::http://ww1.microchip.com/downloads/en/DeviceDoc/xc${_number_of_bits}-v$pkgver-full-install-linux-installer.run")
noextract=('installerBlobFromMicrochip')
md5sums=('443c0b7045bf414aaccb64b91036aac8')
install=$pkgname.install

build() {
  sdx.kit mksplit installerBlobFromMicrochip
  cp /opt/tcl-tabs/tabs-cli-cfs.tcl .
  ./tabs-cli-cfs.tcl unwrap -file *.head -output unpacked.vfs

  #now reassemble files larger than 5MB in the archive, which were split up for whatever reason
  for f in `find ./unpacked.vfs -name '*___bitrockBigFile1'`
  do
    firstChunk="$f"
    baseName="${firstChunk//___bitrockBigFile1/}"
    allPieces="$(find -path "${baseName}*" | sort --version-sort)"
    cat $allPieces > "$baseName".reassembled
    rm $allPieces
    mv "$baseName".reassembled "$baseName"
  done
}

package() {
  install -Dm755 unpacked.vfs/compiler/programfiles/bin/* -t "$pkgdir"/opt/$pkgname/bin
  rm -rf unpacked.vfs/compiler/programfiles/bin
  mv unpacked.vfs/compiler/programfiles/* "$pkgdir"/opt/$pkgname/.

  mkdir -p "$pkgdir/etc/profile.d"
  echo "export PATH="'$PATH'":/opt/${pkgname}/bin" > "$pkgdir/etc/profile.d/${pkgname}.sh"
  echo "export XC${_number_of_bits}_TOOLCHAIN_ROOT=/opt/${pkgname}" >> "$pkgdir/etc/profile.d/${pkgname}.sh" 
 
  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  ln -s "$pkgdir"/docs/license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
